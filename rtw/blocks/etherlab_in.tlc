%implements "etherlab_in" "C"

%%
%% This TLC Implements the necessary IO Functions to communicate
%% with the EtherLAB Generic IO Layer
%%
%% Copyright (C) 2006
%% Richard Hacker
%% IgH Essen GmbH
%% 
%% License: GPL

%include "ETL.tlc"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function BlockTypeSetup( block, system ) void
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %<InitETL()>
  %<LibAddToCommonIncludes("rt_vars.h")>
%endfunction


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function BlockInstanceSetup( block, system ) void
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%endfunction


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function Start(block, system) Output
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %assign dtype = GetEtlDTypeName(LibBlockOutputSignalDataTypeId(0))
  %assign tid = LibGetGlobalTIDFromLocalSFcnTID(0)
  %assign tid = tid ? tid - LibGetTID01EQ() : 0

  /* Input RT Variable %<Type> Block: %<VarName> 
   * Registering destination variable with EtherLAB IO Signal Layer
   */
  if ((%<ETL.ErrStr> = rt_var_reg_input(&rt_app, "%<VarName>", %<dtype>, \
            %<LibBlockOutputSignalWidth(0)>, \
            %<tid>, \
            &%<LibBlockOutputSignal(0,"","",0)>, \
            &%<LibBlockOutputSignal(1,"","",0)>))) {
        snprintf(%<ETL.ErrMsg>, sizeof(%<ETL.ErrMsg>), 
           "Input Signal %<VarName> could not be registered: %s",
           %<ETL.ErrStr>);
        %<LibSetRTModelErrorStatus( "%<ETL.ErrMsg>")>;
  }
%endfunction


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function Outputs( block, system ) Output
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %% The outputs get written directly in the copy function
%endfunction


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function GetEtlDTypeName( dtype ) void
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %switch dtype
    %case tSS_BOOLEAN
      %return "pd_boolean_T"
    %case tSS_UINT8
      %return "pd_uint8_T"
    %case tSS_INT8
      %return "pd_sint8_T"
    %case tSS_UINT16
      %return "pd_uint16_T"
    %case tSS_INT16
      %return "pd_sint16_T"
    %case tSS_UINT32
      %return "pd_uint32_T"
    %case tSS_INT32
      %return "pd_sint32_T"
    %case tSS_DOUBLE
      %return "pd_double_T"
    %case tSS_SINGLE
      %return "pd_single_T"
    %default
      %error Unknown PDODataType: %<dtype>; Known types: ...
                %<tSS_UINT8> %<tSS_INT8> %<tSS_UINT16> %<tSS_INT16> ...
                %<tSS_UINT32> %<tSS_INT32> %<tSS_BOOLEAN> ...
                %<tSS_DOUBLE> %<tSS_SINGLE>
  %endswitch
%endfunction
