%%
%% Generic Routines to handle EtherCAT
%% 
%% Copyright (C) 2006
%% Richard Hacker
%% IgH Essen GmbH
%% 
%% License: GPL

%addincludepath ".."
%include "ETL.tlc"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function InitEtherCAT() void
%% This function checks whether the EtherCAT system has been called once
%% before already, which is demonstrated by the existance of variable 
%% ::EtherCAT. If not, include C-header file for EtherCAT, 
%% and declare a variable where the slave's data structure is placed 
%% by EtherCAT when registering the slaves during mdlStart()
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  %if EXISTS(CompiledModel.EtherCAT)
    %return
  %endif

  %trace "Initializing EtherCAT Interface"

  %<InitETL()>

  %addtorecord CompiledModel EtherCAT { ...
              MasterSampleTime [] ...
              Pdo "pdo" ...
              Slave "slave" ...
              SlaveOwner "" ...
              PDO_Map {} ...
              SlaveCount 0 ...
              model_c LibGetModelDotCFile() ...
              }

  %with CompiledModel
    %assign NumSt = LibNumDiscreteSampleTimes()
    %if FEVAL("verLessThan","simulinkcoder","7.3")
      %% In releases prior to 7.3, LibNumDiscreteSampleTimes() incorrectly
      %% reported the number of sample times.
      %assign NumSt = NumSt - LibGetTID01EQ()
    %endif

    %% EtherCAT header
    %<LibAddToCommonIncludes("ecrt_support.h")>
    %<LibAddToCommonIncludes("pdserv/pdserv.h")>
  
    %openfile buf
    /* Zero terminated list of sample times for EtherCAT */
    unsigned int ec_st[] = {
    %foreach tid = NumSt
        %assign EtherCAT.MasterSampleTime = EtherCAT.MasterSampleTime + 0.0
        %<CAST("Unsigned", ...
               LibGetClockTickStepSize(tid + LibGetTID01EQ())*1000000000 + 0.5)>,
    %endforeach
        0U };
    %closefile buf
    %<LibMdlStartCustomCode(buf, "header")>

    %trace "LibIsSingleTasking %<LibIsSingleTasking()> LibIsSingleRateModel %<LibIsSingleRateModel()> LibGetTID01EQ %<LibGetTID01EQ()>"
    %openfile buf
  
    /* Initialising EtherCAT Support system */
    if ((%<ETL.ErrStr> = ecs_init( ec_st, %<LibIsSingleTasking()>))) {
        snprintf(%<ETL.ErrMsg>, sizeof(%<ETL.ErrMsg>),
                 "Could not initialise EtherCAT Support layer: %s",
                 %<ETL.ErrStr>);
        %<LibSetRTModelErrorStatus("%<ETL.ErrMsg>")>;
        return;
    }
    %closefile buf
    %<LibMdlStartCustomCode(buf, "execution")>
  
    %openfile buf
  
    /* Starting EtherCAT subsystem */
    if ((%<ETL.ErrStr> = ecs_start())) {
          snprintf(%<ETL.ErrMsg>, sizeof(%<ETL.ErrMsg>), 
             "Starting EtherCAT subsystem failed: %s", %<ETL.ErrStr>);
          %<LibSetRTModelErrorStatus("%<ETL.ErrMsg>")>;
          return;
    }
    %closefile buf
    %<LibMdlStartCustomCode(buf, "trailer")>
  
    %openfile buf
  
    /* Shutting down EtherCAT subsystem */
    ecs_end();
    %closefile buf
    %<LibMdlTerminateCustomCode(buf, "execution")>
  
  %endwith

%endfunction

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function GetEtherCATId()
%% Every EtherCAT slave calls InitEtherCAT, where a unique Id is generated
%% per slave. This function returns the Id
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %assign CompiledModel.EtherCAT.SlaveCount = ...
              CompiledModel.EtherCAT.SlaveCount + 1
  %return CompiledModel.EtherCAT.SlaveCount
%endfunction

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function InitEtherCATTask( block, system) void
%% Make sure the domain for the block's sample time exists
%% Return: Idx : index in EtherCAT.Task[Idx]
%%                      correspondig to block's Tid
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  %with EtherCAT

    %%%trace "LibIsSingleTasking %<LibIsSingleTasking()>
    %%%trace "LibIsSingleRateModel %<LibIsSingleRateModel()>"
    %%%trace "LibNumAsynchronousSampleTimes %<LibNumAsynchronousSampleTimes()>"
    %%%trace "LibNuEmDiscreteSampleTimes %<LibNumDiscreteSampleTimes()>"

    %%%foreach tid = LibNumDiscreteSampleTimes()
    %%    %trace CONCAT("======= Sample time %<tid>", ...
    %%           "LibIsContinuous %<LibIsContinuous(tid)>", ...
    %%           "LibIsDiscrete %<LibIsDiscrete(tid)>", ...
    %%           "LibTriggeredTID %<LibTriggeredTID(tid)>", ...
    %%           "LibGetClockTickStepSize %<LibGetClockTickStepSize(tid)>", ...
    %%           "LibAsynchronousTriggeredTID %<LibAsynchronousTriggeredTID(tid)>", ...
    %%           "LibGetClockTickDataTypeId %<LibGetClockTickDataTypeId(tid)>")
    %%%endforeach 

    %% Get RTW Task ID of the block
    %assign tid = LibGetGlobalTIDFromLocalSFcnTID(0)

    %% Calculate EtherLab task id. RTW has the ugliness of allocating
    %% more than one task with the same sample time - these are discarded
    %% for EtherLab
    %assign ETL_tid = tid ? tid - LibGetTID01EQ() : 0

    %% And the Step Size thereof
    %assign Ts = LibGetClockTickStepSize(tid)

    %if MasterSampleTime[ETL_tid]
      %return ETL_tid
    %endif

    %% No domain exists with this Task Id

    %assign MasterSampleTime[ETL_tid] = Ts

    %trace "Added new Task for SampleTime %<Ts> @ %<ETL_tid>"

    %if LibIsContinuous(0)
      %assign SendCondition = "%<LibIsSampleHit(tid)> && %<LibIsMajorTimeStep()>"
    %else
      %assign SendCondition = "%<LibIsSampleHit(tid)>"
    %endif

    %openfile buf

    /* EtherCAT Process for Sample Time [%<Ts>] */
    if (%<SendCondition>) {
      ecs_receive(%<ETL_tid>);
      #ifdef ASYNC_ECAT
      ecs_send(%<ETL_tid>);
      #endif
    }
    %closefile buf
    %<LibSystemOutputCustomCode(system,buf,"execution")>

    %openfile buf

    /* EtherCAT Queue for Sample Time [%<Ts>] */
    #ifndef ASYNC_ECAT
    if (%<SendCondition>) {
      ecs_send(%<ETL_tid>);
    }
    #endif
    %closefile buf
    %<LibSystemUpdateCustomCode(system,buf,"trailer")>

  %endwith

  %return ETL_tid
%endfunction
