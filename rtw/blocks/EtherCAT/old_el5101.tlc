%implements "old_el5101" "C"

%%
%% Hardware Driver for Beckhoff's EtherCAT Incremental Encoder Input 
%% device EL5101
%% 
%% This TLC Implements the necessary IO Functions to communicate
%% with the EtherCAT Driver enabling the usage of an Incremental Encoder Input
%%
%% Copyright (C) 2006
%% Richard Hacker
%% IgH Essen GmbH
%% 
%% License: GPL

%include "EtherCAT.tlc"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function BlockTypeSetup( block, system ) void
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %<InitEtherCAT()>
%endfunction


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function BlockInstanceSetup( block, system ) void
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %<InitEtherCATTask(block, system)>
%endfunction


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function Start(block, system) Output
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  /* %<Type> Block: %<Name> 
   * Registering analog input with EtherCAT Driver
   */
  %<RegisterEtherCATSimplePDO(block, "Beckhoff_EL5101", \
      "Beckhoff_EL5101_PDO_Value", \
      MasterId, SlaveAddr, 0, LibBlockPWork(INC_Word,"","",0))>
  %if StatusOp
    %<RegisterEtherCATSimplePDO(block, "Beckhoff_EL5101", \
        "Beckhoff_EL5101_PDO_Status", \
        MasterId, SlaveAddr, 0, LibBlockPWork(StatusByte,"","",0))>
  %endif
%endfunction


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function Outputs( block, system ) Output
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  /* %<Type> Block: %<Name> 
   */
  %%
  %if StatusOp
  /* %<Type> Block: %<Name> */
  %assign s0 = LibBlockOutputSignal(1,"","",0)
  %assign u0 = "*(uint8_T *)(%<LibBlockPWork(StatusByte,"","",0)>)"
  %<s0> = %<u0>;  /* Status */
  %endif
  %%
  %assign y0 = LibBlockOutputSignal(0,"","",0)
  %if RawOp
    %assign u0 = "*(uint16_T *)(%<LibBlockPWork(INC_Word,"","",0)>)"
  %else
    %assign s0 = "*(uint16_T *)(%<LibBlockPWork(INC_Word,"","",0)>)"
    %if ScaleOp
      %assign m = "%<LibBlockParameter(FullScale,"","",0)>/65536"
      %assign c = "+ %<LibBlockParameter(Offset,"","",0)>"
    %else
      %assign m = 1.0/65536
      %assign c = ""
    %endif
    %assign u0 = "%<m>*%<s0> %<c>"
  %endif
  %<y0> = %<u0>;  /* Data */
  %return
%endfunction
