%implements "generic_ethercat" "C"

%%
%% Generic EtherCAT slave
%%
%% Copyright (C) 2007, Florian Pose
%% 
%% License: GPL
%%

%include "EtherCAT.tlc"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function BlockTypeSetup( block, system ) void
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %<InitEtherCAT()>
%endfunction

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function BlockInstanceSetup( block, system ) void
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %<InitEtherCATTask(block, system)>
%endfunction

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function Start(block, system) Output
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  /* %<Type> Block: %<Name> 
   * Registering Generic EtherCAT slave with EtherCAT Driver
   */
  %<RegisterEtherCATSlaveBlock(block, "%<Vendor>, %<Product>", \
        MasterIndex, SlaveAddress, 0)>

  %foreach portIdx = NumberOfOutputs
    /* Register block output %<portIdx> */
    %<RegisterEtherCATSlavePDO(block, \
        "%<OutputIndices[portIdx]>, %<OutputSubIndices[portIdx]>", \
        LibBlockPWork(TxPdo, "", "", portIdx))>
  %endforeach

  %foreach portIdx = NumberOfInputs
    /* Register block input %<portIdx> */
    %<RegisterEtherCATSlavePDO(block, \
        "%<InputIndices[portIdx]>, %<InputSubIndices[portIdx]>", \
        LibBlockPWork(RxPdo, "", "", portIdx))>
  %endforeach
%endfunction

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function Outputs( block, system ) Output
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  /* %<Type> Block: %<Name> 
   * Generic EtherCAT slave I/O.
   */

  %foreach portIdx = NumberOfOutputs
    /* Output %<portIdx> */
    %assign BlockOutput = LibBlockOutputSignal(portIdx, "", "", 0)
    %assign DataType = OutputTypes[portIdx]
    %assign TxPdoPtr = LibBlockPWork(TxPdo, "", "", portIdx)
    %<BlockOutput> = *((%<DataType> *) %<TxPdoPtr>);
  %endforeach

  %foreach portIdx = NumberOfInputs
    /* Input %<portIdx> */
    %assign DataType = InputTypes[portIdx]
    %assign RxPdoPtr = LibBlockPWork(RxPdo, "", "", portIdx)
    %assign BlockInput = LibBlockInputSignal(portIdx, "", "", 0)
    *(%<DataType> *) %<RxPdoPtr> = %<BlockInput>;
  %endforeach
%endfunction

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function Update(block, system) Output
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %return
%endfunction

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%function Derivatives(block, system) Output
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %return
%endfunction
